#!/usr/bin/env perl
# try -h
# Make a "patch" in a project, extract it to a lib, with skeleton .cpp


use strict; use warnings; no warnings 'uninitialized'; use 5.010; no if ($^V ge v5.18.0), warnings => 'experimental::smartmatch';
use File::Glob ':bsd_glob';
use File::Basename qw(basename dirname);
use File::Spec;
use Cwd;
use IO::File;
use JSON;
use awgrover::Getopt;
use Carp;

our $workspace = "/home/".$ENV{'USER'}.'/xod';
our $switches;
our %cppType = (
    string => 'XString',
    number => 'int|uint8_t|float',
    boolean => 'Logic|boolean',
    pulse => 'Logic|boolean',
    );

sub debug(&) {
    warn "[DEBUG ". (caller(0))[2]."] ". &{$_[0]}."\n" if $ENV{'DEBUG'};
    }

sub warning {
    warn "WARNING ",@_,"\n";
    }

sub find_project {
    my ($want) = @_;
    debug { "Look for project in $workspace for $want/project.xod..."; };
    # FIXME strip project.xod, strip workspace, ... or converse rather
    # If it's a patch, strip it back to project
    # allow relative to here (./)
    if (-e "$workspace/$want/project.xod") {
        debug { "  found project '$workspace'/'$want'" };
        "$workspace/$want";
        }
    else {
        debug { "  not found project '$want'" };
        undef;
        }
    }

sub find_patch {
    my ($project, $want) = @_;
    debug { "Look for patch '$project' / '$want'..."; };
    my $full_path = !$project ? $want : "$project/$want"; # allows ./
    debug { "  start with path $full_path"; };
    if (-d $full_path) {
        debug { "  it's a dir..." };
        $full_path .= "/patch.xodp"
        }

    if ($full_path =~ /\.xodp$/) {
        debug { "  at $full_path?" };
        if (-e $full_path) {
            debug { "  found patch $full_path" };
            return $full_path
            }
        else {
            debug { "  no patch at path $full_path" };
            return undef;
            }
        }

    $full_path .= ".xodp";
    debug { "  at + .xodp?" };
    if (-e $full_path) { 
        debug { "  found patch $full_path" };
        return $full_path
        }
    else {
        debug { "  no patch at path $full_path" };
        return undef;
        }
    }

sub search_project {
    my ($want) = @_;
    debug { "Search in $workspace for ~$want/project.xod..."; };
    my $i=0;
    foreach (bsd_glob( "$workspace/*$want*/project.xod", GLOB_NOCASE )) {
        if (!$i) {
            say "## Maybe you meant projects:";
            $i=1;
            }
        my $project = dirname($_);
        $project =~ s/^\Q$workspace\E\///;
        say "\t$project";
        }
    warn "No projects found\n" if $i==0;
    }

sub patch_to_lib {
    # project patch lib # project/patch -> lib
    # patch lib # patch -> lib # deduce project from path

    # FIXME: if 2 then patch, new
    # if 3 then project patch new
    debug { "ARGV '".join("' '",@ARGV)."'" };

    my $expected_more = sub {
        # debug { "test argv ".scalar(@ARGV)." '".join("' '",@ARGV)."'" };
        if (@ARGV==0) {
            warn "Expected patchpath,newlibpath or projectpath,patchpath,newlibpath\n";
            exit 1;
            }
        };

    &$expected_more();

    my $full_patch_path;

    # $patch_path, $newpath
    if (@ARGV==2 || @ARGV==1) {
        $full_patch_path = find_patch_or_failsearch(undef, $ARGV[0]);
        }
    if ($full_patch_path) {
        debug { 'ARGV was $patch_name, $newpath' };
        shift @ARGV; # so next is newlib
        }

    else {
        debug { 'ARGV must be $project_path, $patch_name, $newpath' };

        # find or search...
        my $project_path = $ARGV[0];
        my $full_project_path = find_project($project_path);
        if (!$full_project_path) {
            warning "No such project $project_path";
            search_project($project_path); # FIXME: should do phonetic/typo matching
            exit(1);
            }
        shift @ARGV;

        &$expected_more();

        # search patchname->fullpath
        my $patch_name = $ARGV[0];
        $full_patch_path = find_patch_or_failsearch($full_project_path, $patch_name);
        shift @ARGV;
        }
    debug { "Have patch $full_patch_path" };

    &$expected_more();

    # check newpath in lib, maybe warn
    my $newlibpath = shift @ARGV;
    # __lib__/$name/$path, $name/$path (in __lib__), existing, no $name part, no $path part
    # with .cpp or .xodp
    # ./.... obviates __lib__/ test

    debug { "Figure new lib path..." };
    if ($newlibpath =~ /\.(cpp|xodp)/) {
        debug { "  strip .cpp/.xodp file parth" };
        $newlibpath = dirname($newlibpath);
        }

    if ($newlibpath !~ /^__lib__\// && ($newlibpath =~ /^\.\// || (-d $newlibpath)) ) {
        # relative, or existing, so I guess you mean it
        # But if you say __lib__/...., and it's relative to cwd(), then you don't "really mean it"
        # no fixup
        debug { "  newlib .||-d: '$newlibpath'..." };
        my $canonpath = Cwd::abs_path($newlibpath); # assume this is the salient path # was File::Spec->canonpath( $newlibpath );
        $canonpath =~ /__lib__\//;
        my ($beforelib, $justlibpath) = ($`.$& || ($& ? '' : $newlibpath."/"),$');
        my $partct = split('/', $justlibpath);
        if ($newlibpath =~ /^__lib__(\/|$)/ || $partct != 3 ) {
            my @possible = ('$owner', '$libname', '$patchname');
            debug { "    partct $partct of $justlibpath of canon $canonpath" };
            my $meantmore = join("/", @possible[ ($partct)..$#possible ]);
            warning "You probably meant $beforelib$meantmore, because this is not deep enough under __lib__/";
            }
        }
    else {
        if ($newlibpath !~ /^__lib__\//) {
            $newlibpath = "__lib__/$newlibpath" 
            }
        my $actualworkspace = (-d $newlibpath) ? "." : $workspace; # could be relative to here

        debug { "  in workspace: $newlibpath..." };
        my $partct = split('/', $newlibpath) - 1;
        if ($partct == 2) {
            $newlibpath = "$newlibpath/".basename(dirname($full_patch_path));
            debug { "  imply patch name from source patch name " };
            $partct = 3;
            }
        if ($partct != 3) {
            warn 'libraries have three level names: $owner/$libname/$patchname'."\n";
            if (-d $newlibpath) {
                warn "maybe you meant one of\n";
                foreach ( bsd_glob("$actualworkspace/$newlibpath/*") ) {
                    $_ =~ s/^\Q$actualworkspace\E\/__lib__\///;
                    warn "\t$_\n";
                    }
                warn "\tor $newlibpath/\$somepatchname\n";
                }
            exit(1);
            }
        $newlibpath = "$actualworkspace/$newlibpath";
        }

    debug { "Into lib $newlibpath..." };
    system('mkdir', '-p', $newlibpath) && exit($?);

    my ($patch_decl, $has_native, $inputs, $outputs) = process_patchfile( $full_patch_path );

    make_patchfile($newlibpath, $full_patch_path, $patch_decl, $has_native);

    if (!$switches->{'patch-only'}) {
        mv_cpp($newlibpath, $full_patch_path, $has_native);
        }
    rename_nodes( $newlibpath, $full_patch_path ); 
    }

sub rename_nodes {
  # Fix all refernces to the patch in the old project to the lib
  my ($newlibpath, $full_patch_path) = @_;
  my $patch_dir = dirname($full_patch_path);
  my $libpatchdir = dirname($newlibpath);

  # The patches in $full_patch_path/.. are @/basename()
  my $nodename = basename($patch_dir);
  my $project = dirname($patch_dir);
  my $libname = basename(dirname($libpatchdir))."/".basename($libpatchdir);
  my $lib_patch_name = "$libname/$nodename";
  debug {"Change old @/$nodename to $lib_patch_name"};

  my $patch_ct = 0;
  foreach my $patch ( glob("$project/*/patch.xodp") ) {
    $patch_ct += 1;
    my $xodp = xodp( $patch );
    my $changed = 0;
    for my $node (@{$xodp->{'nodes'}}) {
      next if $node->{'type'} ne "@/$nodename";
      $node->{'type'} = $lib_patch_name;
      $changed += 1;
      }
    if ($changed) {
      if (!$switches->{'n'}) {
        my $fh = IO::File->new( $patch, 'w') || die($!);
        say $fh to_json_string($xodp);
        $fh->close;
        }
      }
      warn "#" .($switches->{'n'} ? "(didn't)" : '') . " update $changed type in $patch\n" if $changed;
    }
  debug { "Checked $patch_ct patches"};


  # don't overwrite existing
  }

sub mv_cpp {
  # just copy the patch.cpp
  my ($newlibpath, $full_patch_path, $has_native) = @_;
  if ( -e "$full_patch_path/patch.cpp" ) {
    if (!$switches->{'patch-only'}) {
      system('mv', "$full_patch_path/patch.cpp", "$newlibpath");
      }
    warn "#" .($switches->{'n'} ? "(didn't)" : '') . " move to $newlibpath/patch.cpp";
    }
  }

sub emit_inputs_outputs {
    my ($inputs, $outputs) = @_;

    say "## inputs" if @$inputs;
    foreach my $in (@$inputs) {
        say getValue($in)." ".is_dirty($in);
        }

    say "## outputs" if @$outputs;
    foreach my $out (@$outputs) {
        say emitValue($out)
        }
    }

sub xodp {
  my ($patch_path) = @_;
  local @ARGV = $patch_path;
  confess "no such $patch_path" if ! -e $patch_path;
  decode_json(join("",<>));
  }

sub process_patchfile {
    my ($full_patch_path) = @_;

    my $patch_decl = xodp($full_patch_path);
    # warn "json: ".Dumper($patch_decl)." "; use Data::Dumper;
    debug { "Read $full_patch_path: ". join(" ", sort keys %$patch_decl) . ": ".substr($patch_decl->{'description'},0,100) };

    my $has_native = 0;
    my $has_non_xod_patch = 0;
    my @inputs;
    my @outputs;
    debug { "Nodes..." };
    foreach ( @{ $patch_decl->{'nodes'} ||[]} ) {
        $has_native +=1 if $_->{'type'} eq 'xod/patch-nodes/not-implemented-in-xod';
        $has_non_xod_patch +=1 if $_->{'type'} !~ /^xod\/patch-nodes/;
        if ($_->{'type'} =~ /^xod\/patch-nodes\/input-(.*)/) {
            my $type = $1;
            push @inputs, { type => $type, name => $_->{'label'} };
            }
        if ($_->{'type'} =~ /^xod\/patch-nodes\/output-(.*)/) {
            my $type = $1;
            push @outputs, { type => $type, name => $_->{'label'} };
            }
        }
    my $has_links = @{ ($patch_decl->{'links'} || []) };

    debug { "  has native $has_native" };
    debug { "  has non i/o $has_non_xod_patch" };
    debug { "  has links ".$has_links };
    debug { "  has inputs ".@inputs.": ".join(" ", map { $_->{'name'} } @inputs) };
    debug { "  has outputs ".@outputs.": ".join(" ", map { $_->{'name'} } @outputs) };
    # warn Dumper(\@inputs); use Data::Dumper;

    my @fails;

    warning "Has no inputs (xod/patch-nodes/input-*), which doesn't make sense" if (!@inputs);
    warning "Has no outputs xod\/patch-nodes/output-*, which might be ok" if !@outputs;

    if ($has_native && $has_links) {
        push @fails, "Has native node (xod/patch-nodes/not-implemented-in-xod) but also links, which doesn't make sense (in project's patch $full_patch_path)";
        }
    if ($has_native > 1) {
        push @fails, "Has $has_native > 1 native nodes (xod/patch-nodes/not-implemented-in-xod), which doesn't make sense (in project's patch $full_patch_path)";
        }
    if (!$has_native && !$has_links) {
        push @fails, "Has no native node, and no links, which doesn't make sense (in project's patch $full_patch_path)";
        }
    if ($has_native && $has_non_xod_patch) {
        push @fails, "Has native and non 'xod/patch-nodes/*' nodes, which doesn't make sense (in project's patch $full_patch_path)";
        }
    if ( !$has_native && -e "$full_patch_path/patch.cpp" ) {
        push @fails, "Has no native but has patch.cpp, which doesn't make sense (in project's patch $full_patch_path)";
        }
    warning "Has native but no patch.cpp" if ( $has_native && !-e "$full_patch_path/patch.cpp" );

    if (@fails) {
        warn "FAIL\n".join("\n", @fails)."\n" ;
        exit(1);
        }
    return ($patch_decl, $has_native, \@inputs, \@outputs);
    }

sub to_json_string {
  my ($xodp) = @_;
  # canonical, etc. "standard xodp"
  to_json($xodp, {utf8=>1, pretty => 1, canonical=>1, indent_length =>2});
  }

sub make_patchfile {
    # copy the .xodp to new location
    my ($newlibpath, $full_patch_path, $patch_decl, $has_native) = @_;

    my $patchfile = "$newlibpath/".basename($full_patch_path);
    debug { "  will create ".basename($patchfile) };
    
    warning "Needs Description" if !$patch_decl->{'description'};

    if (! $switches->{'n'}) {
      system( 'mv', $full_patch_path, $patchfile );
      }
    warn "#" .($switches->{'n'} ? "(didn't)" : '') . " move to $patchfile\n";
    }

sub make_cpp {
    # use skeleton to create a .cpp in the $newlibpath
    my ($newlibpath, $full_patch_path, $has_native, $inputs, $outputs) = @_;

    if ($has_native) {
        # make .cpp template
        backup_skeleton( "$newlibpath/any.cpp" );

        my $fh = IO::File->new( "$newlibpath/any.cpp", 'w') || die($!);
        print $fh skeleton_cpp($full_patch_path, $inputs, $outputs); 
        $fh->close if !(-e "$newlibpath/any.cpp");

        if (!revert_backup_ifsame( "$newlibpath/any.cpp" )) {
            warn "# wrote $newlibpath/any.cpp\n";
            }
        else {
            warn "# unchanged $newlibpath/any.cpp\n";
            }
        }
    else {
        warning "Unneeded .cpp in $newlibpath" if bsd_glob( "$newlibpath/*.cpp" );
        }

    # could do git checks
    }

sub largest_n_skeleton_bak {
    my ($fname) = @_;
    my $n = 0;
    foreach ( bsd_glob( "$fname.*.bak" ) ) {
        $n = 0;
        if (/(\d+)\.bak$/) {
            $n = $1 if ($1 > $n);
            }
        }
    return $n;
    }

sub backup_skeleton {
    my ($fname) = @_;
    return if !-e $fname;
    return if -z $fname;

    my $n = largest_n_skeleton_bak($fname) + 1;
    system( 'cp', $fname, "$fname.$n.bak" );
    debug { "backup $fname to $fname.$n.bak" };
    }

sub revert_backup_ifsame {
    my ($fname) = @_;
    return undef if !-e $fname;

    my $n = largest_n_skeleton_bak($fname);
    return undef if !-e "$fname.$n.bak";
    if ( ! system( 'diff', '-q', $fname, "$fname.$n.bak" ) ) { # not diff
        debug { "revert $fname to $fname.$n.bak" };
        system( 'mv', "$fname.$n.bak", $fname );
        return 1;
        }
    return undef;
    }

sub getValue {
    # format the getvalue statement
    my ($input_info) = @_;
    sprintf( '%s newValue = getValue<input_%s>(ctx); // %s', ($cppType{$input_info->{'type'}} || 'auto'), $input_info->{'name'}, $input_info->{'type'} );
    }

sub is_dirty {
    my ($input_info) = @_;
    # formatting
    sprintf( 'if (isInputDirty<input_%s>(ctx)) {', $input_info->{'name'} );
    }

sub emitValue {
    my ($out_info) = @_;
    sprintf( 'emitValue<output_%s>(ctx, /* a \'%s\' %s */);', $out_info->{'name'}, $out_info->{'type'}, ($cppType{$out_info->{'type'}} || '?') );
    }

sub skeleton_cpp {
    my ($full_patch_path, $inputs, $outputs) = @_;
    my $template = join("", <DATA>);
    
    $template =~ s/\$FROM/$full_patch_path/g;
    my $now = gmtime()." (utc)";
    $template =~ s/\$DATE/$now/g;

    my @emits;
    foreach my $out (@$outputs) {
        push @emits, '    '.emitValue($out);
        }

    my $insert_vars = sub {
        my $indent = $1;
        my @intext;
        foreach my $in (@$inputs) {
            my $var = "input_".$in->{'name'};
            push @intext, $indent.is_dirty($in);
            if ($in->{'type'} eq 'pulse') {
                push @intext, sprintf( $indent.'    // pulse. unwrap the \'if\' for implicit "is dirty this pulse"' );
                }
            else {
                push @intext, $indent.'    '.getValue($in);
                }
            push @intext, map { "$indent$_" } @emits;
            if (!@$outputs) {
                push @intext, $indent.'    /* Do Something */';
                }
            push @intext,$indent."    }";
            push @intext,'';
            }
        
        join( "\n",@intext );
        };
    $template =~ s/^([ \t]*)\$EACHIN/&$insert_vars()/egm;

    if (@$outputs) {
        $template =~ s/^(\s*)\$GEN_EMIT/"$1".join("\n$1",@emits)/egm;
        }
    else {
        $template =~ s/^(\s*)\$GEN_EMIT/$1\/* Do Something because at least one person was dirty *\//gm;
        }
    return $template;
    }


sub find_patch_or_failsearch {
    my ($project_path, $patch_name) = @_;
    my $found = find_patch($project_path, $patch_name);
    return $found if $found;

    $patch_name = "$project_path/$patch_name" if $project_path;
    debug { "Search for patches '$patch_name'..." };

    # proj/patch/x.xodp
    if ($patch_name =~ /\.xodp$/) {
        $patch_name = dirname($patch_name);
        debug { "  trim off file part: $patch_name..." };
        }

    # proj/*.xod
    if ($patch_name =~ /\.xod$/ ) {
        $patch_name = dirname($patch_name)."/*";
        debug { "  trim off file part: $patch_name..." };
        }

    # proj/patch
    my ($projpart,$patchpart) = (basename(dirname($patch_name)), basename($patch_name));
    # $projpart = '*' if $projpart eq '.';
    return if $projpart ne '.' &&
        (_find_patch_or_failsearch("$workspace/$projpart/*$patchpart*") ||
        _find_patch_or_failsearch("$workspace/*$projpart*/*$patchpart*") ||
        _find_patch_or_failsearch("$workspace/$projpart/*") ||
        _find_patch_or_failsearch("$workspace/*$projpart*/*") ||
        _find_patch_or_failsearch("$workspace/*/$patchpart*") ||
        _find_patch_or_failsearch("$workspace/*/*$patchpart*")
        ) && exit(1);

    # patch
    (_find_patch_or_failsearch("$workspace/*/$patchpart") ||
    _find_patch_or_failsearch("$workspace/*/*$patchpart*")
    ) && exit(1);

    _find_patch_or_failsearch("$workspace/*/*") && exit(1);
    warn("No patches found\n");
    exit(1)
    }

sub _find_patch_or_failsearch {
    my ($search) = @_;

    debug { "  try '$search'..." };

    my $i=0;
    foreach (bsd_glob("$search/*.xodp")) {
        if (!$i) {
            say "## Maybe you meant patches:";
            $i=1;
            }
        my $where = dirname($_);
        $where =~ s/^\Q$workspace\E\///;
        say "\t".$where;
        }
    return $i!=0;

    }

sub do_mv {
    my ($old, $new) = @_;
    die "Expected \$old \$new\n" if !$new || !$old;

    # paths are __lib__/$ns/$cat/$lib
    # or $ns/$cat/$lib
    $old =~ s/\/$//; # we rely on "clean" name
    $new =~ s/\/$//; # we rely on "clean" name

    my $new_lib_name = $new."";
    $new_lib_name =~ s/^(\.\/)?__lib__\///; # canonical w/o lib/
    debug { "new_lib_name '$new_lib_name'" };
    my $new_path = "__lib__/$new_lib_name";
    if ( split('/', $new_lib_name) != 3) {
       die "Unlikely new-name, expected __lib__/\$ns/\$cat/\$lib or \$ns/\$cat/\$lib\n";
       }

    warning "Already exists: ./$new_path" if (-e $new_path); # relative
    warning "Already exists: $workspace/$new_path" if (-e "$workspace/$new_path"); # relative

    my $old_lib_name = $old."";
    $old_lib_name =~ s/^(\.\/)?__lib__\///; # canonical w/o __lib__/
    debug { "old_lib_name '$old_lib_name'" };
    my $old_path = "__lib__/$old_lib_name";
    my $last_real_path;
    for my $prefix ( ( ".", "$workspace" )) {
        my $extant = "$prefix/$old_path";
        next if ! -e $extant;
        if ( $last_real_path eq Cwd::abs_path( $extant ) ) {
            warning "skipped: $extant is the same as $last_real_path";
            next;
            }
        $last_real_path = Cwd::abs_path( $extant );

        debug { "Extant old $extant..." };
        debug { "mv $extant $prefix/$new_path" };
        # system('mv', $extant, "$prefix/$new_path" ) && die($?);
        }
    
    debug { "relink lib-edit project" };
    system('make', 'lib2patch') && die($?);

    my $esc_old = $old_lib_name."";
    $esc_old =~ s/\//\\\//g;
    my $esc_new = $new_lib_name."";
    $esc_new =~ s/\//\\\//g;
    debug { "Edit $old_lib_name -> $new_lib_name" };
    foreach my $file ( `find $workspace $workspace/lib ./__lib__ ./examples -name '*.xodp'` ) {
        chomp $file;
        system('grep', '-H', '-n', "\"$old_lib_name\"", "$file")
        || system( 'sed', '-i', "s/\"$esc_old\"/\"$esc_new\"/g", "$file");
        }
    }

sub main {
    given ($ARGV[0]) {
        when ('mv') {
            shift @ARGV;
            do_mv( @ARGV );
            exit 0;
            }
        when ('template') {
          shift @ARGV;
          my $where = "$workspace/".(shift @ARGV);
          say $where;
          my ($patch_decl, $has_native, $inputs, $outputs) = process_patchfile( "$where/patch.xodp" );
          make_cpp($where, $where, 1, $inputs, $outputs);
          emit_inputs_outputs( $inputs, $outputs );
          exit 0;
          }

        when (/^-/) {
            # ok
            }
        default {
            # ok
            }
        }
    
   $switches=awgrover::Getopt::GetOptions
                (
                '' => <<'EOS',
This is a mess, does a bunch of stuff to make a patch from a template, etc.
--- OBSOLETE, use `script/mv`. 'mv' $old $new # -> rename extant library patch, paths are $author/$libname/$patchname
--- 'template' $project/$patch # -> $project/$patch/patch.cpp # skeleton from patch.xodp inputs/outputs
# Make a lib from a patch, either in cwd, or installed xod
--- [options] $project $patch $libpath # project/patch -> libname. relative means to here, __lib__/... means ~/xod
      $project # path in ~/xod. so normally just the name
      $patch # dir in project, or dir/something.xodp
--- [options] $patch-path $newlibpath # same, but path-to-patch -> libname
      $patch-path # path to patch directory or .xodp
EOS
                'help|h|H'=>'this',
                'patch-only|xodp-only|p' => 'Only remake the .xodp, leave the cpp alone',
                'n' => "don't make changes",
                );

    patch_to_lib;
    }

main();

# Template for .cpp, see skeleton_cpp()
__DATA__
// Converted from the patch $FROM

{{#global}}
//#include <Servo.h>
{{/global}}

struct State {
    // Number sample = NAN;
};

{{ GENERATED_CODE }}

void evaluate(Context ctx) {
    // State* state = getState(ctx);
    
    $EACHIN

    // if (newValue != state->sample) // only emit if state changed
    // if (getValue<output_OUT>(ctx) != state->sample) // only emit if state changed

    $GEN_EMIT

    state->sample = newValue; // save new state
}

